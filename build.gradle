plugins {
  id "java"
  id "distribution"
}

version = 'v3.4'

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'lib')
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

distributions {
    main {
        baseName = 'StatAlign'
        contents {
	    from ('examples') { into ('examples') }
	    from ('scripts') { into ('scripts') }
	    from ('lib/mpj-v0_44') { into ('lib/mpj-v0_44') }
	    from ('.') { 
		include 'LICENSE.txt', 'CHANGELOG.txt', 'README.md', 'StatAlignParallel' 
	    }
	    from jar
        }
    }
}

jar {
    baseName = 'StatAlign'
    archiveName = 'StatAlign.jar'
    manifest {
        attributes "Main-Class": "statalign.StatAlign"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' 
}
